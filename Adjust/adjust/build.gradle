apply plugin: 'com.android.library'

def getVersionName() {
    return "4.12.0"
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 26
        versionCode 1
        versionName getVersionName()
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task clearJar(type: Delete) {
    delete "build/outputs/adjust-${getVersionName()}.jar"
}

task makeJar(type: Copy) {
    from('build/intermediates/bundles/default/')
    into('build/outputs/')
    include('classes.jar')
    rename ('classes.jar', "adjust-${getVersionName()}.jar")
}

makeJar.dependsOn(clearJar, build)

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
